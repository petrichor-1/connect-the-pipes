Import "secretblocks.htn"
Import "./animation.htn"

Scene game:
	square win_screen:
		Game.ðŸ“£show_win_buttons = "Show win buttons"
		Game.ðŸ“£show_stars = "show stars"
		custom_rule hide_on_play
		When game_starts:
			set_invisibility(percent: 100)
			check_once_if(Self.total_clones == 1):
				set_position(to_x: Game.width / 2, y: Game.height / 2)
				create_a_clone_of_this_object
				set_width(Game.width, height: Game.height)
				set_color(h(0, s: 0, b: 90))
		When object_is_cloned:
			set_text(to: "Level complete!", color: h(Game.primary_color_hue, s: 5, b: 5))
			wait(seconds: 0)
			set_position(to_x: Game.width / 2, y: Game.height * 0.8 + Self.height + 8)
		When i_get_a_message(Game.ðŸ“£Success):
			bring_to_front
			custom_block ease_invisibility(from: 100, to: 20, milliseconds: 1000)
			broadcast_message(named: Game.ðŸ“£show_stars)
			wait(seconds: 1.5)
			broadcast_message(named: Game.ðŸ“£show_win_buttons)

	text win_screen_tap_count_display(text: "0 taps", resize_scale: 0.75):
		custom_rule hide_on_play
		When game_starts:
			set_invisibility(percent: 100)
		When i_get_a_message(Game.ðŸ“£Success):
			set_text(to: join(Game.total_taps, with: " taps"), color: h(Game.primary_color_hue, s: 5, b: 5))
			wait(seconds: 0)
			set_origin(to_x: max(64, Game.width / 2 - 320), y: Game.height * 0.8 - 64)
			bring_to_front
			custom_block ease_invisibility(from: 100, to: 0, milliseconds: 900)

	star win_screen_stars:
		custom_rule hide_on_play
		When game_starts:
			set_invisibility(percent: 100)
			check_once_if(Self.total_clones == 1):
				create_clones_of_this_object(times: 2)
		When i_get_a_message(Game.ðŸ“£show_stars):
			# Technically this means that depending on rng a 3 star finish may be impossible,
			#     but that is not consistent between plays so it is acceptable
			three_star_taps = Game.board_width * Game.board_height * 2
			two_star_taps = three_star_taps + Game.board_width * Game.board_height / 2
			one_star_taps = two_star_taps + Game.board_width * Game.board_height
			stars = 0
			check_if_else(Game.total_taps <= three_star_taps):
				stars = 3
			else:
				check_if_else(Game.total_taps <= two_star_taps):
					stars = 2
				else:
					check_once_if(Game.total_taps <= one_star_taps):
						stars = 1
			star_offset = Self.clone_index - 1
			final_size = 100 - absolute_value(star_offset) * 10
			final_x = Game.width / 2 + star_offset * 128
			final_y = Game.height / 2 - absolute_value(star_offset) * 8
			repeat(times: Self.clone_index):
				wait_milliseconds(500)
			set_size(percent: 0)
			set_invisibility(percent: 0)
			bring_to_front
			set_color(h(Game.primary_color_hue, s: Game.primary_color_saturation, b: Game.primary_color_brightness + 50))
			check_if_else(stars > Self.clone_index):
				custom_block ease_position_and_size(from_x: Game.width / 2, to_x: final_x, from_y: Game.height / 2, to_y: final_y, from_size: 0, to_size: final_size, milliseconds: 500)
			else:
				set_invisibility(percent: 100)
				set_position(to_x: final_x, y: final_y)
				set_size(percent: final_size * 0.9)
				set_color(h(Game.primary_color_hue, s: Game.primary_color_saturation / 2, b: Game.primary_color_brightness))
				custom_block ease_invisibility(from: 100, to: 10, milliseconds: 500)
	
	triangle next_level_button:
		custom_rule hide_on_play
		When game_starts:
			set_invisibility(percent: 100)
			set_position(to_x: Game.width / 2 + 182, y: Game.height * 0.2)
			set_color(h(Game.primary_color_hue, s: 5, b: 5))
			check_once_if(Self.total_clones == 1):
				create_clones_of_this_object(times: 2)
				set_angle(-90)
		When object_is_cloned:
			check_if_else(Self.clone_index == 1):
				set_text(to: "â—¯", color: h(Game.primary_color_hue, s: 5, b: 5))
				set_position(to_x: Self.x_position - 10, y: Self.y_position + 22)
				set_size(percent: 300)
			else:
				set_image(square)
				set_width(182, height: 182)
		When i_get_a_message(Game.ðŸ“£show_win_buttons):
			bring_to_front
			# Just the last clone will be 99% invisible. This lets it be tapped but not seen
			custom_block ease_invisibility(from: 100, to: 99 * max(0, Self.clone_index - 1), milliseconds: 250)
		When is_tapped(Self):
			increase(Game.current_level_index, by: 1)
			broadcast_message(named: Game.ðŸ“£play)

	text replay_level_button(text: "â†º"):
		custom_rule hide_on_play
		When game_starts:
			set_invisibility(percent: 100)
			set_position(to_x: Game.width / 2, y: Game.height * 0.2)
			set_color(h(Game.primary_color_hue, s: 5, b: 5))
			check_once_if(Self.total_clones == 1):
				create_clones_of_this_object(times: 2)
				set_size(percent: 200)
		When object_is_cloned:
			check_if_else(Self.clone_index == 1):
				set_text(to: "â—¯", color: h(Game.primary_color_hue, s: 5, b: 5))
				set_position(to_x: Self.x_position, y: Self.y_position + 22)
				set_size(percent: 300)
			else:
				set_image(square)
				set_width(182, height: 182)
		When i_get_a_message(Game.ðŸ“£show_win_buttons):
			bring_to_front
			# Just the last clone will be 99% invisible. This lets it be tapped but not seen
			custom_block ease_invisibility(from: 100, to: 99 * max(0, Self.clone_index - 1), milliseconds: 250)
		When is_tapped(Self):
			broadcast_message(named: Game.ðŸ“£play)

custom_rule hide_on_play:
	When i_get_a_message(Game.ðŸ“£play):
		set_invisibility(percent: 100)